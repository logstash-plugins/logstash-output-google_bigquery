import java.nio.file.Files
import java.nio.file.Paths

import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group "org.logstash.outputs"
version Files.readAllLines(Paths.get("version")).first()

repositories {
    mavenCentral()
}


dependencies {
    // Core dependency for BigQuery clients
    implementation "com.google.cloud:google-cloud-bigquery:1.137.2"

    // Dependencies of com.google.cloud:google-cloud-bigquery:1.24.1
    implementation "com.fasterxml.jackson.core:jackson-core:2.13.3" // this is required since Logstash 8.3.0
    implementation "com.google.apis:google-api-services-bigquery:v2-rev20210726-1.32.1"
    implementation "com.google.api:api-common:2.0.0"
    implementation "com.google.api-client:google-api-client:1.32.1"
    implementation "com.google.api:gax:2.0.0"
    implementation "com.google.api:gax-httpjson:0.85.0"
    implementation "com.google.api.grpc:proto-google-common-protos:2.3.2"
    implementation "com.google.api.grpc:proto-google-iam-v1:1.0.14"
    implementation "com.google.apis:google-api-services-storage:v1-rev20210127-1.32.1"
    implementation "com.google.auth:google-auth-library-credentials:1.0.0"
    implementation "com.google.auth:google-auth-library-oauth2-http:1.0.0"
    implementation "com.google.cloud:google-cloud-core:2.0.2"
    implementation "com.google.cloud:google-cloud-core-http:2.0.2"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.google.code.gson:gson:2.8.7"
    implementation "com.google.errorprone:error_prone_annotations:2.8.1"
    implementation "com.google.guava:guava:30.1.1-jre"
    implementation "com.google.http-client:google-http-client:1.39.2"
    implementation "com.google.http-client:google-http-client-appengine:1.39.2"
//    implementation "com.google.http-client:google-http-client-jackson:1.23.0" // it's needed?
    implementation "com.google.http-client:google-http-client-gson:1.39.2"
    implementation "com.google.http-client:google-http-client-jackson2:1.39.2"
    implementation "com.google.j2objc:j2objc-annotations:1.3"
    implementation "com.google.oauth-client:google-oauth-client:1.31.5"
    implementation "com.google.protobuf:protobuf-java:3.17.3"
    implementation "com.google.protobuf:protobuf-java-util:3.17.3"
    implementation "commons-codec:commons-codec:1.15"
    implementation "commons-logging:commons-logging:1.2"
    implementation "io.grpc:grpc-context:1.39.0"
    implementation "io.opencensus:opencensus-api:0.28.0"
    implementation "io.opencensus:opencensus-contrib-http-util:0.28.0"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
//    implementation "joda-time:joda-time:2.9.2" // it's needed?
    implementation "org.apache.httpcomponents:httpclient:4.5.13"
    implementation "org.apache.httpcomponents:httpcore:4.4.14"
//    implementation "org.checkerframework:checker-compat-qual:2.5.2" // it's needed?
//    implementation "org.codehaus.jackson:jackson-core-asl:1.9.11" // it's needed?
//    implementation "org.codehaus.mojo:animal-sniffer-annotations:1.14" // it's needed?
    implementation "org.threeten:threetenbp:1.5.1"
}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-output-google_bigquery_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
            w << "require_jar(\'${project.group}\', \'${project.name}\', \'${project.version}\')\n"
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.allDependencies.each { dep ->
            File f = configurations.runtimeClasspath.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
        String projectGroupPath = project.group.replaceAll('\\.', '/')
        File projectJarFile = file("${vendorPathPrefix}/${projectGroupPath}/${project.name}/${project.version}/${project.name}-${project.version}.jar")
        projectJarFile.mkdirs()
        Files.copy(file("$buildDir/libs/${project.name}-${project.version}.jar").toPath(), projectJarFile.toPath(), REPLACE_EXISTING)
    }
}

vendor.dependsOn(jar, generateGemJarRequiresFile)